######################## relation definitions
# EDB
.input participants `repeat_iter(peers.clone()) -> map(|p| (p,))`
.input numParticipants `repeat_iter([(num_participants,),])`

.input clientIn `repeat_iter(vec![()]) -> map(|_| (context.current_tick() as u64, context.current_tick() as u32))`
.output clientOut `for_each(|(tid,payload):(u64,u32)| println!("completed {:?}: {:?}", tid, payload))`

.input periodic `source_stream(periodic_source) -> map(|_| ())`
.output throughputOut `for_each(|(num,):(u32,)| println!("total_throughput,{:?}", num))`

.async voteToParticipant `map(|(node_id, v):(u32,(u64,u32))| (node_id, serialize_to_bytes(v))) -> dest_sink(vote_to_participant_sink)` `null::<(u64,u32,)>()`
.async voteFromParticipant `null::<(u64,u32,u32,)>()` `source_stream(vote_from_participant_source) -> map(|v| deserialize_from_bytes::<(u64,u32,u32,)>(v.unwrap()).unwrap())`
.async commitToParticipant `map(|(node_id, v):(u32,(u64,u32))| (node_id, serialize_to_bytes(v))) -> dest_sink(commit_to_participant_sink)` `null::<(u64,u32,)>()`
.async ackFromParticipant `null::<(u64,u32,u32,)>()` `source_stream(ack_from_participant_source) -> map(|v| deserialize_from_bytes::<(u64,u32,u32,)>(v.unwrap()).unwrap())`

.output logCommit `map(|(tid,p):(u64,u32)| format!("tid: {:?}, p: {:?}", tid, p)) -> dest_sink(file_sink)`
######################## end relation definitions

# Phase 1a
voteToParticipant@addr(tid, p) :~ participants(addr), clientIn(tid, p)

# Phase 1b, Phase 2a
AllVotes(tid, payload, src) :+ AllVotes(tid, payload, src), !committed(tid, _)
AllVotes(tid, payload, src) :- voteFromParticipant(tid, payload, src)

NumYesVotes(tid, count(src)) :- AllVotes(tid, payload, src)
committed(tid, payload) :- NumYesVotes(tid, num), AllVotes(tid, payload, src), numParticipants(num) 
logCommit(tid, payload) :- committed(tid, payload)
logCommitComplete(tid, payload) :+ committed(tid, payload)
commitToParticipant@addr(tid, payload) :~ logCommitComplete(tid, payload), participants(addr)

# Phase 2b
AllAcks(tid, payload, src) :+ AllAcks(tid, payload, src), !completed(tid, _)
AllAcks(tid, payload, src) :- ackFromParticipant(tid, payload, src)

NumAcks(tid, count(src)) :- AllAcks(tid, payload, src)
completed(tid, payload) :- NumAcks(tid, num), AllAcks(tid, payload, src), numParticipants(num)
// clientOut(tid, payload) :- completed(tid, payload)

NumCompleted(count(tid)) :- completed(tid, payload)
totalCompleted(new) :+ !totalCompleted(prev), NumCompleted(new)
totalCompleted(prev) :+ totalCompleted(prev), !NumCompleted(new)
totalCompleted(prev + new) :+ totalCompleted(prev), NumCompleted(new)
throughputOut(num) :- totalCompleted(num), periodic()