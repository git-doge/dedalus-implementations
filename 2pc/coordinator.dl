.input clientIn
.output clientOut

.output voteToParticipant
.input voteFromParticipant
.output instructToParticipant
.input ackFromParticipant

.output log_instruct
.input instruct_logged

unanimous(size)

commitInstruct(type)
rollbackInstruct(type)

success(res)
reject(res)

AllMsg(msg) :+ AllMsg(msg)
AllVotes(i, msg, res, l_from) :+ AllVotes(i, msg, res, l_from)
AllAcks(i, msg, l_from) :+ AllAcks(i, msg, l_from)
message(i, msg) :+ message(i, msg)

AllMsg(msg) :-
    clientIn(msg)

NextMsgToAssign(choose(msg)) :-
    AllMsg(msg),
    !message(_, msg)

NextID(max(i)) :-
    message(i, _)

message(i, msg) :+
    NextMsgToAssign(msg),
    NextID(j),
    i == j + 1

// should i initialize NextToGo with 0?
NextToGo(i) :-
    lastMsgFinalized(j, _),
    i == j + 1

voteToParticipant@addr(i, msg) :~
    participantAddr(addr), # send to all participants
    message(i, msg), # getting the next message
    NextToGo(i) # everything before it must be fully resolved

AllVotes(i, msg, res, l_from) :-
    voteFromParticipant(i, msg, res, l_from)

NumYesVotes(i, msg, count(l_from)) :-
    AllVotes(i, msg, res, l_from),
    success(res)

VotedNo(i, msg) :-
    AllVotes(i, msg, res, l_from),
    reject(res)

# message is committed
msgCommitted(i, msg) :-
    NumYesVotes(i, msg, size),
    unanimous(size)

log_instruct(i, msg, type) :- # commit record forced to stable storage
    msgCommitted(i, msg),
    commitInstruct(type)

# message is rolled back
msgAborted(i, msg) :-
    VotedNo(i, msg)

log_instruct(i, msg, type) :- # rollback record forced to stable storage
    msgAborted(i, msg),
    rollbackInstruct(type)

# send instruct message to participants
instructToParticipant@addr(i, msg, type) :~
    participantAddr(addr),
    instruct_logged(i, msg, type) # send instruct to participant after logging locally

# collecting acks before moving on
AllAcks(i, msg, l_from) :-
    ackFromParticipant(i, msg, l_from)

NumAcks(i, msg, count(l_from)) :-
    AllAcks(i, msg, l_from)

lastMsgFinalized(max(i), msg) :-
    NumAcks(i, msg, size),
    unanimous(size)